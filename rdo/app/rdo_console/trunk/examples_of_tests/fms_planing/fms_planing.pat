/*$Pattern  qwerty : operation
$Relevant_resources
	_Деталь         :  Детали          Keep Keep 
	_Станок         :  Станки          Keep Keep 
$Time = 10
$Body
	_Деталь
	Choice from _Деталь.состояние == Пришла
	first
	Convert_begin
			состояние = В_обработке;
	Convert_end
			номер_операции++;

	_Станок
	Choice from _Станок.состояние_станка == Свободен and Тип_станка (_Деталь.номер_операции, _Деталь.номер) == _Станок.тип
	first
	Convert_begin 
		состояние_станка = Занят;		
	Convert_end 
		состояние_станка = Свободен;	
$End*/

$Pattern Образец_установки_и_базирования1 : operation  // установка и базирование
$Relevant_resources
	_Деталь         :  Детали          Keep Keep 
	_Станок         :  Станки          Keep Keep 
	_Приспособление :  Приспособления  Keep Keep  
$Time = Длительность_базирования (_Деталь.номер, _Станок.номер_текущей_детали)
$Body

    _Деталь
	Choice from _Деталь.состояние == Налажена 
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = Установка_базирование;		 
	Convert_end
		состояние = Установлена;
		
    _Станок
	Choice from _Станок.состояние_станка == Свободен and Тип_станка (_Деталь.номер_операции, _Деталь.номер) == _Станок.тип and До_конца_смены_осталось (time_now) >= Длительность_базирования (_Деталь.номер, _Станок.номер_текущей_детали)
	with_min (_Станок.количество_обработанных )  //first 	
	Convert_begin 
		состояние_станка = Установка_базирование;
		номер_текущей_детали = _Деталь.номер;
		номер_текущей_операции = _Деталь.номер_операции;		
	Convert_end 
		состояние_станка = Подготовлен;	
		
_Приспособление
	Choice from _Приспособление.состояние == Налажено and _Приспособление.номер_установленной_детали == _Деталь.номер 
	Convert_begin
		состояние = Установка_базирование;		
	Convert_end
		состояние = Установлено;		
$End

$Pattern Образец_установки_и_базирования2 : operation  // установку и базирование не производим, так как время на эту операцию > чем время, оставшееся до конца смены
$Relevant_resources
	_Деталь         :  Детали          Keep Keep 
	_Станок         :  Станки          Keep Keep 
	_Приспособление :  Приспособления  Keep Keep 
$Time = До_конца_смены_осталось (time_now) - 30 
$Body

_Деталь
	Choice from _Деталь.состояние == Налажена 
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = Простой;		 
	Convert_end
		состояние = Налажена;
		
_Станок
	Choice from _Станок.состояние_станка == Свободен and Тип_станка (_Деталь.номер_операции, _Деталь.номер) == _Станок.тип and До_конца_смены_осталось (time_now) < Длительность_базирования (_Деталь.номер, _Станок.номер_текущей_детали)
	with_min (_Станок.количество_обработанных )  //first 	
	Convert_begin 
		состояние_станка = Простой;	
		номер_текущей_детали = _Деталь.номер;
		номер_текущей_операции = _Деталь.номер_операции;		
	Convert_end 
		состояние_станка = Свободен;	
		
_Приспособление
	Choice from _Приспособление.состояние == Налажено and _Приспособление.номер_установленной_детали == _Деталь.номер
	Convert_begin
		состояние = Простой;		
	Convert_end 
		состояние = Налажено;		
$End

$Pattern Образец1_обработки_детали : operation // используем в том случае, когда время обработки меньше, чем время до конца смены
$Relevant_resources
	_Деталь          :  Детали          Keep Keep // обработка 
	_Станок          :  Станки          Keep Keep 
	_Приспособление  :  Приспособления  Keep Keep // обработка 
$Time = Длительность_обработки (_Деталь.номер_операции, _Деталь.номер)
$Body

_Деталь
	Choice from _Деталь.состояние == Установлена and До_конца_смены_осталось (time_now) >= Длительность_обработки (_Деталь.номер_операции, _Деталь.номер)
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = В_обработке;		
	Convert_end
		состояние = Пришла;
		номер_операции++;
		
_Станок
	Choice from _Станок.состояние_станка == Подготовлен and _Станок.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	with_min (_Станок.количество_обработанных )  //first 	
	Convert_begin
		состояние_станка = Занят;	
		номер_текущей_детали = _Деталь.номер;
		номер_текущей_операции = _Деталь.номер_операции;
	Convert_end  
		состояние_станка = Свободен;
		количество_обработанных++; 
		
_Приспособление
	Choice from _Приспособление.состояние == Установлено and _Приспособление.номер_установленной_детали  == _Деталь.номер
	Convert_begin
		состояние = Обработка;		
	Convert_end
		состояние = Свободно;
$End

$Pattern Образец1_наладки_детали : operation // используем в том случае, когда время обработки меньше, чем время до конца смены
$Relevant_resources
	_Деталь         :  Детали          Keep Keep // наладка
	_Приспособление :  Приспособления  Keep Keep // наладка  
$Time =  Длительность_наладки (_Деталь.номер) 
$Body
		 
_Деталь
	Choice from _Деталь.состояние == Пришла
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = В_наладке;
	Convert_end 
		состояние = Налажена; 
		
_Приспособление
	Choice from _Приспособление.состояние == Свободно and _Приспособление.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	first
	Convert_begin
		состояние = Наладка;		
	Convert_end
		состояние = Налажено;
		номер_установленной_детали = _Деталь.номер; 

$End

$Pattern Образец2_обработки_детали : operation // используем в том случае, если время на обработку больше времени, оставшегося до конца смены, а время на переналадку - меньше
$Relevant_resources											 // наладку производим, но станок простаивает
	_Деталь         :  Детали          Keep Keep // обработка
	_Станок         :  Станки          Keep Keep 
	_Приспособление :  Приспособления  Keep Keep // обработка
$Time = 0                                 
$Body

_Деталь
	Choice from _Деталь.состояние == Установлена and До_конца_смены_осталось (time_now) <= Длительность_обработки (_Деталь.номер_операции, _Деталь.номер) 
	first
	Convert_begin 
		состояние = Простой;	 
	Convert_end
		состояние = Установлена;    // простаивает
		
_Станок
	Choice from _Станок.состояние_станка == Подготовлен and _Станок.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	with_min (_Станок.количество_обработанных )  //first 	
	Convert_begin
		состояние_станка = Простой;	
		номер_текущей_детали = _Деталь.номер;
		номер_текущей_операции = _Деталь.номер_операции;		
	Convert_end  
		состояние_станка = Подготовлен;     // простаивает	
		
_Приспособление
	Choice from _Приспособление.состояние == Установлено and _Приспособление.номер_установленной_детали  == _Деталь.номер
	Convert_begin
		состояние = Простой;		
	Convert_end
		состояние = Установлено;     // простаивает 		

$End

$Pattern Образец2_наладки_детали : operation // используем в том случае, если время на обработку больше времени, оставшегося до конца смены, а время на переналадку - меньше
$Relevant_resources											 // наладку производим, но станок простаивает
	_Деталь         :  Детали          Keep Keep // наладка
	_Приспособление :  Приспособления  Keep Keep // наладка  
$Time =  Длительность_наладки (_Деталь.номер)                                     
$Body

_Деталь
	Choice from _Деталь.состояние == Пришла and До_конца_смены_осталось (time_now) >= Длительность_наладки (_Деталь.номер)
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = В_наладке;		
	Convert_end
		состояние = Налажена; 
		
_Приспособление
	Choice from _Приспособление.состояние == Свободно and _Приспособление.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	first
	Convert_begin
		состояние = Наладка;		
	Convert_end
		состояние = Налажено;
		номер_установленной_детали = _Деталь.номер; 		

$End

$Pattern Образец3_обработки_детали : operation // используем в том случае, если время на наладку больше, чем осталось времени до конца смены
$Relevant_resources											 // не производим ни наладку, ни обработку
	_Деталь         :  Детали          Keep Keep // обработка
	_Станок         :  Станки          Keep Keep 
	_Приспособление :  Приспособления  Keep Keep // обработка
$Time =  До_конца_смены_осталось (time_now) - 30                                   
$Body
 
_Деталь
	Choice from _Деталь.состояние == Установлена 
	first
	Convert_begin 
		состояние = Простой;	 
	Convert_end
		состояние = Установлена;  // простаивает

_Станок
	Choice from _Станок.состояние_станка == Подготовлен and _Станок.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	with_min (_Станок.количество_обработанных )  //first 	
	Convert_begin
		состояние_станка = Простой;
		номер_текущей_детали = _Деталь.номер;
		номер_текущей_операции = _Деталь.номер_операции;		
	Convert_end  
		состояние_станка = Подготовлен;  // простаивает
		
_Приспособление
	Choice from _Приспособление.состояние == Установлено and _Приспособление.номер_установленной_детали  == _Деталь.номер
	Convert_begin
		состояние = Простой;		
	Convert_end
		состояние = Установлено;     // простаивает

$End


$Pattern Образец3_наладки_детали : operation // используем в том случае, если время на наладку больше, чем осталось времени до конца смены
$Relevant_resources											 // не производим ни наладку, ни обработку
	_Деталь         :  Детали          Keep Keep // наладка
	_Приспособление :  Приспособления  Keep Keep // наладка  
$Time =  До_конца_смены_осталось (time_now) - 30                                   
$Body
		
_Деталь
	Choice from _Деталь.состояние == Пришла and До_конца_смены_осталось (time_now) <= Длительность_наладки (_Деталь.номер)
	with_min (_Деталь.позиция_в_очереди)
	Convert_begin 
		состояние = Простой;		
	Convert_end
		состояние = Пришла;    // простаивает
		
_Приспособление
	Choice from _Приспособление.состояние == Свободно and _Приспособление.тип == Тип_станка (_Деталь.номер_операции, _Деталь.номер)
	first
	Convert_begin
		состояние = Простой;		
	Convert_end 
		состояние = Свободно;

$End
$Pattern  Образец_ухода_детали: rule
$Relevant_resources
	_Деталь  :  Детали   Erase
$Body
_Деталь  
	Choice from Тип_станка(_Деталь.номер_операции, _Деталь.номер) == нет
	Convert_rule
$End 
