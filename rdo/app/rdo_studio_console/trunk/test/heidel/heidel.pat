



$Pattern Образец_генерация_бревен  : rule   trace
$Relevant_resources
  система       : Распилочная_машина_1   Keep
  транспортер   : Транспортер            Keep
  новое_бревно  : Бревно                 Create
$Body
  система
    Choice NoCheck first
    Convert_rule
      счетчик_бревен   = система.счетчик_бревен + 1;
  транспортер
    Choice from транспортер.счетчик > 0
    first
    Convert_rule
      текущее_кол_бревен = транспортер.текущее_кол_бревен + 1;
      счетчик          = транспортер.счетчик - 1;
      ген_длина        = Длина_бревна(транспортер.код);
      ген_диаметр      = Диаметр_бревна(транспортер.код);
  новое_бревно
    Convert_rule   trace
      номер            = система.счетчик_бревен;
      длина            = транспортер.ген_длина;
      диаметр_н        = транспортер.ген_диаметр;
      диаметр_к        = транспортер.ген_диаметр - (транспортер.ген_длина * 10) / 100;
      объем_кубический_м  = Объем_бревна(транспортер.ген_длина, транспортер.ген_диаметр,
                                транспортер.ген_диаметр - (транспортер.ген_длина * 10) / 100);
      нач_длина        = транспортер.ген_длина;
      нач_объем_кубический_м   = Объем_бревна(транспортер.ген_длина, транспортер.ген_диаметр,
                                     транспортер.ген_диаметр - (транспортер.ген_длина * 10) / 100);
      вид_древесины    = транспортер.получить_вид_древесины;
      качество         = транспортер.получить_качество;
      статус           = ничего;
      код_места        = транспортер.код;
      счетчик_оборотов = 0;
      позиция          = 0;
      использование_длины  = 0.0;
      использование_куб_м  = 0.0;
$End

$Pattern Образец_прибытие_заказа  : rule   trace
$Relevant_resources
  некий_заказ      : Позиция_заказа        Keep
  заказ_для_статис : Заказ_для_статистики  Create
$Body
  некий_заказ
    Choice from некий_заказ.статус == обработано
    first
    Convert_rule
      номер_заказа     NoChange;
      номер_позиции    NoChange;
      имя_клиента      = Имя_клиента;
      количество       = Количество_заказов(10, 70);
      дата_обслуживания  = Time_now + Дата_выполнения_заказа (1, 5) * 8.0;
      время_прибытия   = Time_now;
      типоразмер       = Типоразмер_пиломатериала; 
      длина            = Длина_пиломатериала(12, 24) * 25;
      ширина           = Размеры_пиломатериала(_ширина, некий_заказ.типоразмер);
      высота           = Размеры_пиломатериала(_высота, некий_заказ.типоразмер);
      объем            = (некий_заказ.длина / 100.0) * (некий_заказ.ширина / 1000.0) *
                           (некий_заказ.высота / 1000.0) * некий_заказ.количество;
      вид_древесины    = Вид_древесины_заказа;
      класс            = Класс_заказа;
      качество         = Качество_заказа;
      приоритет        = Приоритет_заказа(1, 10);
      кол_готовых      = 0;
      кол_пилоблоков   = 0;
      склад            = проходит;
      статус           = в_обработке;
  заказ_для_статис
    Convert_rule   trace
      номер_заказа     = некий_заказ.номер_заказа;
      номер_позиции    = некий_заказ.номер_позиции;
      дата_обслуживания  = некий_заказ.дата_обслуживания;
      время_прибытия   = Time_now;
      количество       = некий_заказ.количество;
      кол_готовых      = 0;
      кол_пилоблоков   = 0;
      треб_диам_1      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 1) +
                              некий_заказ.длина / 10.0;
      треб_диам_2      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 2) +
                              некий_заказ.длина / 10.0;
      треб_диам_3      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 3) +
                              некий_заказ.длина / 10.0;
      треб_диам_4      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 4) +
                              некий_заказ.длина / 10.0;
      треб_диам_6      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 6) +
                              некий_заказ.длина / 10.0;
      треб_диам_8      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 8) +
                              некий_заказ.длина / 10.0;
      треб_диам_9      = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 9) +
                              некий_заказ.длина / 10.0;
$End

$Pattern Образец_выбор_склада  : rule   trace
$Relevant_resources
  некий_заказ      : Позиция_заказа    Keep
  некоторый_склад  : Склад             Keep
$Body
  некий_заказ
    Choice from некий_заказ.статус == в_обработке and
                некий_заказ.склад == проходит
    first
    Convert_rule
      склад            = некоторый_склад.код;
  некоторый_склад
    Choice from некоторый_склад.код <> Ск_29 and некоторый_склад.код <> Ск_30 and
                некоторый_склад.номер_заказа == 0 and
                некоторый_склад.длина * 100 >= некий_заказ.длина
    first
    Convert_rule
      номер_заказа     = некий_заказ.номер_заказа;
      номер_позиции    = некий_заказ.номер_позиции;
$End

$Pattern  Образец_Бревно_переходит_Т2_К2  : operation  trace
$Relevant_resources
  транспортер       : Транспортер_2    Keep      Keep
  некоторое_бревно  : Бревно           Keep      Keep
  конвейер          : Конвейер_2       Keep      Keep
  показать_бревно   : Бревно_показать  Keep      Keep
$Time = Время_передачи_бревна
$Body
  транспортер
    Choice from транспортер.текущее_кол_бревен > 0 and
                транспортер.статус == остановлен
    first
    Convert_begin
      статус           = вперед;
    Convert_end
      текущее_кол_бревен  = транспортер.текущее_кол_бревен - 1;
      статус           = остановлен;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Тр_2
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Кн_2;
      счетчик_оборотов = конвейер.счетчик_оборотов;
      позиция          = конвейер.позиция + 900;
  конвейер
    Choice from конвейер.текущее_кол_бревен == 0 and
                конвейер.движение == нет and
                Конвейер_1.текущее_кол_бревен == 0
    first
    Convert_begin
      текущее_кол_бревен  = конвейер.текущее_кол_бревен + 1;
      статус           = остановлен;
    Convert_end
      шаг              = Конв_2_нормальный_шаг;
      статус           = вперед;
  показать_бревно
    Choice from показать_бревно.номер == 0
    first
    Convert_begin
      номер            = некоторое_бревно.номер;
      длина            = 0;
    Convert_end
      длина            = некоторое_бревно.длина;
      код_места        = некоторое_бревно.код_места;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
$End

$Pattern  Образец_Бревно_переходит_Т1_К1 : operation  trace
$Relevant_resources
  транспортер       : Транспортер_1    Keep      Keep
  некоторое_бревно  : Бревно           Keep      Keep
  конвейер          : Конвейер_1       Keep      Keep
  показать_бревно   : Бревно_показать  Keep      Keep
$Time = Время_передачи_бревна
$Body
  транспортер
    Choice from транспортер.текущее_кол_бревен > 0 and
                транспортер.статус == остановлен
    first
    Convert_begin
      статус           = вперед;
    Convert_end
      текущее_кол_бревен  = транспортер.текущее_кол_бревен - 1;
      статус           = остановлен;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Тр_1
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Кн_1;
      счетчик_оборотов = конвейер.счетчик_оборотов;
      позиция          = конвейер.позиция + 500;
  конвейер
    Choice from конвейер.текущее_кол_бревен == 0 and
                конвейер.движение == нет and
                Конвейер_2.текущее_кол_бревен == 0
    first
    Convert_begin
      текущее_кол_бревен  = конвейер.текущее_кол_бревен + 1;
      статус           = остановлен;
    Convert_end
      статус           = вперед;
  показать_бревно
    Choice from показать_бревно.номер == 0
    first
    Convert_begin
      номер            = некоторое_бревно.номер;
      длина            = 0;
    Convert_end
      длина            = некоторое_бревно.длина;
      код_места        = некоторое_бревно.код_места;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
$End

$Pattern  Образец_Бревно_переходит_К1_К2  : operation  trace
$Relevant_resources
  некоторое_бревно      : Бревно            Keep      Keep
  конвейер_откуда       : Конвейер_1        Keep      Keep
  конвейер_куда         : Конвейер_2        NoChange  Keep
  показать_бревно       : Бревно_показать   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_1 and
                Конвейер_1.позиция - некоторое_бревно.позиция -
                (Конвейер_1.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_1.длина <= 0
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Кн_2;
      счетчик_оборотов = конвейер_куда.счетчик_оборотов;
      позиция          = конвейер_куда.позиция;
  конвейер_откуда
    Choice from конвейер_откуда.текущее_кол_бревен > 0 and
                конвейер_откуда.движение == нет
    first
    Convert_begin
      статус           = остановлен;
    Convert_end
      текущее_кол_бревен  = конвейер_откуда.текущее_кол_бревен - 1;
      статус           = вперед;
  конвейер_куда
    Choice from конвейер_куда.текущее_кол_бревен == 0
    first
    Convert_end
      текущее_кол_бревен  = конвейер_куда.текущее_кол_бревен + 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторое_бревно.номер
    first
    Convert_end
      код_места        = некоторое_бревно.код_места;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
$End

$Pattern Образец_начало_измерения  : rule   trace
$Relevant_resources
  позиция_измерения : Позиция_измерения_1   Keep
  некоторое_бревно  : Бревно                Keep
$Body
  позиция_измерения
    Choice from позиция_измерения.состояние == свободен
    first
    Convert_rule
      состояние        = занят;
      длина            = 0;
      диаметр_н        = некоторое_бревно.диаметр_н;
      диаметр_к        = некоторое_бревно.диаметр_н;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == ничего and
                Конвейер_2.позиция - некоторое_бревно.позиция -
                (Конвейер_2.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_2.длина <= ПИ_координата
    first
    Convert_rule
      статус           = измерение;
$End

$Pattern Образец_конец_измерения  : rule   trace
$Relevant_resources
  позиция_измерения   : Позиция_измерения_1    Keep
  некоторое_бревно    : Бревно                 Keep
  распилочная_машина  : Распилочная_машина_1   Keep
  конвейер            : Конвейер_2             Keep
$Body
  позиция_измерения
    Choice from позиция_измерения.состояние == занят
    first
    Convert_rule
      состояние        = свободен;
      длина            = некоторое_бревно.длина;
      диаметр_н        = некоторое_бревно.диаметр_н;
      диаметр_к        = некоторое_бревно.диаметр_к;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == измерение and
                Конвейер_2.позиция - некоторое_бревно.позиция -
                (Конвейер_2.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_2.длина + некоторое_бревно.длина <= ПИ_координата
    first
    Convert_rule
      статус           = раскрой;
  распилочная_машина
    Choice NoCheck first
    Convert_rule
      длина            = некоторое_бревно.длина;
      диаметр_н        = некоторое_бревно.диаметр_н;
      диаметр_к        = некоторое_бревно.диаметр_к;
      объем_кубический_м  = некоторое_бревно.объем_кубический_м;
      длина_брутто     = некоторое_бревно.длина;
      длина_нетто      = 0;
      объем_кубический_м_брутто     = некоторое_бревно.объем_кубический_м;
      объем_кубический_м_нетто      = 0.0;
      длина_для_отпила      = некоторое_бревно.длина - Размер_отпил_торца;
      диаметр_н_для_отпила  = некоторое_бревно.диаметр_н;
  конвейер
    Choice NoCheck first
    Convert_rule
      шаг              = Конв_2_медленный_шаг;
$End

$Pattern Образец_раскрой  : rule   trace
$Relevant_resources
  некоторое_бревно    : Бревно                NoChange
  результат           : Результат_раскроя     Keep
  некий_заказ         : Позиция_заказа        Keep
  заказ_для_статис    : Заказ_для_статистики  Keep
  конвейер            : Конвейер_2            NoChange
  распилочная_машина  : Распилочная_машина_1  Keep
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == раскрой
    first
  результат
    Choice from результат.номер == распилочная_машина.кол_пилоблоков + 1
    first
    Convert_rule
      номер_заказа     = некий_заказ.номер_заказа;
      номер_позиции    = некий_заказ.номер_позиции;
      имя_клиента      = некий_заказ.имя_клиента;
      вид_древесины    = некоторое_бревно.вид_древесины;
      качество         = некий_заказ.качество;
      класс            = некий_заказ.класс;
      длина            = некий_заказ.длина;
      ширина           = некий_заказ.ширина;
      высота           = некий_заказ.высота;
      кол_пилоблоков   = IMin(Получить_кол_брусьев(некий_заказ.типоразмер, некий_заказ.класс,
                             распилочная_машина.диаметр_н_для_отпила - некий_заказ.длина / 10.0),
                             некий_заказ.количество - некий_заказ.кол_готовых);
      тек_диаметр       = распилочная_машина.диаметр_н_для_отпила;
      треб_диам         = Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс,
                             Получить_кол_брусьев(некий_заказ.типоразмер, некий_заказ.класс,
                               распилочная_машина.диаметр_н_для_отпила - некий_заказ.длина / 10.0)) +
                             (некий_заказ.длина * 10) / 100;
      объем_кубический_м  = Объем_бревна(некий_заказ.длина, распилочная_машина.диаметр_н_для_отпила,
                             распилочная_машина.диаметр_н_для_отпила - (некий_заказ.длина * 10) / 100);
      использование       = результат.кол_пилоблоков * (некий_заказ.длина / 100.0 *
                              некий_заказ.ширина / 1000000.0 * некий_заказ.высота) /
                              результат.объем_кубический_м;
      код_склада       = некий_заказ.склад;
      termin           = некий_заказ.дата_обслуживания;
      soll             = некий_заказ.количество;
      lst              = некий_заказ.кол_готовых + результат.кол_пилоблоков;
      накопл_длина     = распилочная_машина.длина_нетто + некий_заказ.длина + Размер_отпил_торца;
  некий_заказ
    Choice from некий_заказ.статус == в_обработке and
                некий_заказ.склад <> проходит and
                некий_заказ.кол_готовых < некий_заказ.количество and
                некий_заказ.длина <= распилочная_машина.длина_для_отпила and
                Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс, 1) +
                  некий_заказ.длина / 10.0 <= распилочная_машина.диаметр_н_для_отпила
    with_max (некий_заказ.длина / 100.0 * некий_заказ.ширина /
             1000000.0 * некий_заказ.высота *
             IMin(Получить_кол_брусьев(некий_заказ.типоразмер, некий_заказ.класс,
               распилочная_машина.диаметр_н_для_отпила - некий_заказ.длина / 10.0),
               некий_заказ.количество - некий_заказ.кол_готовых)) /
             Объем_бревна(некий_заказ.длина, распилочная_машина.диаметр_н_для_отпила,
               распилочная_машина.диаметр_н_для_отпила - (некий_заказ.длина * 10) / 100)

/*    with_min распилочная_машина.диаметр_н_для_отпила - (некий_заказ.длина * 10) / 100 -
             Требуемый_диаметр(некий_заказ.типоразмер, некий_заказ.класс,
               IMin(Получить_кол_брусьев(некий_заказ.типоразмер, некий_заказ.класс,
               распилочная_машина.диаметр_н_для_отпила - некий_заказ.длина / 10.0),
               некий_заказ.количество - некий_заказ.кол_готовых))*/

/*    with_max Случайно(0.0, 1.0)*/

    Convert_rule
      кол_готовых      = некий_заказ.кол_готовых + результат.кол_пилоблоков;
      кол_пилоблоков   = некий_заказ.кол_пилоблоков + 1;
  заказ_для_статис
    Choice from заказ_для_статис.номер_заказа == некий_заказ.номер_заказа and
                заказ_для_статис.номер_позиции == некий_заказ.номер_позиции
    first
    Convert_rule
      кол_готовых      = некий_заказ.кол_готовых;
      кол_пилоблоков   = некий_заказ.кол_пилоблоков;
  конвейер
    Choice from конвейер.текущее_кол_бревен > 0 and
                конвейер.движение == нет
    first
  распилочная_машина
    Choice from распилочная_машина.состояние == свободен
    first
    Convert_rule
      кол_пилоблоков   = распилочная_машина.кол_пилоблоков + 1;
      длина_нетто      = распилочная_машина.длина_нетто + некий_заказ.длина;
      объем_кубический_м_нетто  = распилочная_машина.объем_кубический_м_нетто +
                                    Объем_бревна(некий_заказ.длина, распилочная_машина.диаметр_н_для_отпила,
                                    распилочная_машина.диаметр_н_для_отпила - (некий_заказ.длина * 10) / 100);
      длина_для_отпила      = распилочная_машина.длина_для_отпила - некий_заказ.длина;
      диаметр_н_для_отпила  = распилочная_машина.диаметр_н_для_отпила - (некий_заказ.длина * 10) / 100;
$End

$Pattern Образец_конец_раскроя  : rule   trace
$Relevant_resources
  некоторое_бревно          : Бревно             Keep
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == раскрой
    first
    Convert_rule
      статус             = раскроено;
$End

$Pattern Образец_переключение_назад  : rule   trace
$Relevant_resources
  некоторое_бревно  : Бревно          NoChange
  конвейер          : Конвейер_2      Keep
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == раскроено and
                Конвейер_2.позиция - некоторое_бревно.позиция -
                (Конвейер_2.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_2.длина < Размер_отпил_торца * -1
    first
  конвейер
    Choice from конвейер.статус == вперед and конвейер.движение == нет
    first
    Convert_rule
      статус           = назад;
$End

$Pattern Образец_переключение_стоп  : rule   trace
$Relevant_resources
  некоторое_бревно  : Бревно          Keep
  конвейер          : Конвейер_2      Keep
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == раскроено and
                Конвейер_2.позиция - некоторое_бревно.позиция -
                (Конвейер_2.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_2.длина == Размер_отпил_торца * -1
    first
    Convert_rule
      статус           = готово_к_отпилке_торца;
  конвейер
    Choice from конвейер.движение == нет
    first
    Convert_rule
      статус           = остановлен;
$End

$Pattern Образец_зачистка_торца  : operation   trace
$Relevant_resources
  распилочная_машина : Распилочная_машина_1     Keep      Keep
  некоторое_бревно   : Бревно                   NoChange  Keep
  показать_бревно    : Бревно_показать          NoChange  Keep
  конвейер           : Конвейер_2               NoChange  Keep
$Time = Время_распиловки
$Body
  распилочная_машина
    Choice from распилочная_машина.состояние == свободен
    first
    Convert_begin
      состояние        = занят;
    Convert_end
      состояние        = свободен;
      длина            = распилочная_машина.длина - Размер_отпил_торца;
      диаметр_н        = распилочная_машина.диаметр_н - Размер_отпил_торца * 10 / 100;
      объем_кубический_м  = Объем_бревна(распилочная_машина.длина,
                              распилочная_машина.диаметр_н, распилочная_машина.диаметр_к);
      тек_номер_пилоблоков  = 1;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == готово_к_отпилке_торца
    first
    Convert_end
      длина            = распилочная_машина.длина;
      диаметр_н        = распилочная_машина.диаметр_н;
      объем_кубический_м  = распилочная_машина.объем_кубический_м;
      статус           = позиционируется;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов +
                             Новый_счетчик_оборотов_бревно(некоторое_бревно.позиция,
                             Размер_отпил_торца, конвейер.длина);
      позиция          = Новая_позиция_бревно(некоторое_бревно.позиция,
                             Размер_отпил_торца, конвейер.длина);
  показать_бревно
    Choice from показать_бревно.номер == некоторое_бревно.номер
    first
    Convert_end
      длина            = некоторое_бревно.длина;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
  конвейер
    Choice from конвейер.текущее_кол_бревен > 0 and
                конвейер.статус == остановлен
    first
    Convert_end
      шаг              = Конв_2_нормальный_шаг;
      статус           = вперед;
$End

$Pattern Образец_переключение_позиционировано  : rule   trace
$Relevant_resources
  некоторое_бревно  : Бревно             Keep
  результат         : Результат_раскроя  NoChange
  конвейер          : Конвейер_2         Keep
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == позиционируется and
                Распилочная_машина_1.тек_номер_пилоблоков <= Распилочная_машина_1.кол_пилоблоков
    first
    Convert_rule
      статус           = позиционировано;
  результат
    Choice from результат.номер == Распилочная_машина_1.тек_номер_пилоблоков and
                Конвейер_2.позиция - некоторое_бревно.позиция -
                (Конвейер_2.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_2.длина <= результат.длина * -1
    first
  конвейер
    Choice from конвейер.движение == нет
    first
    Convert_rule
      статус           = остановлен;
$End

$Pattern Образец_распилка  : operation   trace
$Relevant_resources
  конвейер_2                : Конвейер_2            NoChange  NoChange
  результат                 : Результат_раскроя     NoChange  NoChange
  распилочная_машина        : Распилочная_машина_1  Keep      Keep
  некоторое_бревно          : Бревно                Keep      Keep
  показать_бревно           : Бревно_показать       NoChange  Keep
  некоторый_пилоблок_бревна : Пилоблоки             Create    NoChange
  конвейер_3                : Конвейер_3            NoChange  Keep
  новое_показать_бревно     : Бревно_показать       Keep      Keep
$Time = Время_распиловки
$Body
  конвейер_2
    Choice from конвейер_2.текущее_кол_бревен > 0 and
                конвейер_2.статус == остановлен
    first
  результат
    Choice from результат.номер == распилочная_машина.тек_номер_пилоблоков
    first
  распилочная_машина
    Choice from распилочная_машина.состояние == свободен
    first
    Convert_begin
      состояние        = занят;
      счетчик_бревен   = распилочная_машина.счетчик_бревен + 1;
    Convert_end
      состояние        = свободен;
      длина            = распилочная_машина.длина - результат.длина;
      диаметр_н        = распилочная_машина.диаметр_н - результат.длина * 10 / 100;
      объем_кубический_м  = Объем_бревна(распилочная_машина.длина,
                                распилочная_машина.диаметр_н, распилочная_машина.диаметр_к);
      тек_номер_пилоблоков   = распилочная_машина.тек_номер_пилоблоков + 1;
      обработано_пилоблоков  = распилочная_машина.обработано_пилоблоков + 1;
      общий_объем_кубический_м_выхода  = распилочная_машина.общий_объем_кубический_м_выхода + результат.кол_пилоблоков *
                                           (результат.длина / 100.0 * результат.ширина / 1000000.0 *
                                            результат.высота);
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == позиционировано
    first
    Convert_begin
      статус           = распиловка;
    Convert_end
      длина            = распилочная_машина.длина;
      диаметр_н        = распилочная_машина.диаметр_н;
      объем_кубический_м  = распилочная_машина.объем_кубический_м;
      статус           = позиционируется;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов +
                             Новый_счетчик_оборотов_бревно(некоторое_бревно.позиция,
                             результат.длина, конвейер_2.длина);
      позиция          = Новая_позиция_бревно(некоторое_бревно.позиция,
                             результат.длина, конвейер_2.длина);
  показать_бревно
    Choice from показать_бревно.номер == некоторое_бревно.номер
    first
    Convert_end
      длина            = некоторое_бревно.длина;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
  некоторый_пилоблок_бревна
    Convert_begin  trace
      номер            = распилочная_машина.счетчик_бревен;
      длина            = результат.длина;
      диаметр_н        = распилочная_машина.диаметр_н;
      диаметр_к        = распилочная_машина.диаметр_н - результат.длина * 10 / 100;
      объем_кубический_м  = результат.объем_кубический_м;
      вид_древесины    = результат.вид_древесины;
      качество         = результат.качество;
      код_места        = Кн_3;
      позиция          = Конвейер_3.позиция + результат.длина;
      счетчик_оборотов = Конвейер_3.счетчик_оборотов;
      номер_заказа     = результат.номер_заказа;
      номер_позиции    = результат.номер_позиции;
      код_склада       = результат.код_склада;
      кол_пилоблоков   = результат.кол_пилоблоков;
      треб_диам        = результат.треб_диам;
      использование    = результат.использование;
  конвейер_3
    Choice from конвейер_3.текущее_кол_бревен == 0
    first
    Convert_end
      текущее_кол_бревен  = конвейер_3.текущее_кол_бревен + 1;
      статус           = вперед;
  новое_показать_бревно
    Choice from новое_показать_бревно.номер == 0
    first
    Convert_begin
      номер            = распилочная_машина.счетчик_бревен;
      длина            = 0;
    Convert_end
      длина            = некоторый_пилоблок_бревна.длина;
      код_места        = некоторый_пилоблок_бревна.код_места;
      счетчик_оборотов = некоторый_пилоблок_бревна.счетчик_оборотов;
      позиция          = некоторый_пилоблок_бревна.позиция;
$End

$Pattern  Образец_Бревно_переходит_К2_К3  : operation  trace
$Relevant_resources
  некоторое_бревно    : Бревно                Keep      Keep
  конвейер_откуда     : Конвейер_2            Keep      Keep
  конвейер_куда       : Конвейер_3            NoChange  Keep
  показать_бревно     : Бревно_показать       NoChange  Keep
  распилочная_машина  : Распилочная_машина_1  NoChange  Keep
  позиция_измерения   : Позиция_измерения_1   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_2 and
                некоторое_бревно.статус == позиционируется and
                Распилочная_машина_1.тек_номер_пилоблоков > Распилочная_машина_1.кол_пилоблоков
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      статус           = готово;
      код_места        = Кн_3;
      счетчик_оборотов = конвейер_куда.счетчик_оборотов;
      позиция          = конвейер_куда.позиция;
      использование_длины  = (некоторое_бревно.нач_длина - некоторое_бревно.длина) * 1.0 / некоторое_бревно.нач_длина;
      использование_куб_м  = (некоторое_бревно.нач_объем_кубический_м - некоторое_бревно.объем_кубический_м) / некоторое_бревно.нач_объем_кубический_м;
  конвейер_откуда
    Choice from конвейер_откуда.текущее_кол_бревен > 0 and
                конвейер_откуда.движение == нет
    first
    Convert_begin
      текущее_кол_бревен  = конвейер_откуда.текущее_кол_бревен - 1;
      статус           = остановлен;
    Convert_end
      статус           = вперед;
  конвейер_куда
    Choice from конвейер_куда.текущее_кол_бревен == 0
    first
    Convert_end
      текущее_кол_бревен  = конвейер_куда.текущее_кол_бревен + 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторое_бревно.номер
    first
    Convert_end
      код_места        = некоторое_бревно.код_места;
      счетчик_оборотов = некоторое_бревно.счетчик_оборотов;
      позиция          = некоторое_бревно.позиция;
  распилочная_машина
    Choice NoCheck first
    Convert_end
      длина            = 0;
      диаметр_н        = 0;
      диаметр_к        = 0;
      кол_пилоблоков   = 0;
      тек_номер_пилоблоков = 0;
      длина_нетто      = 0;
      объем_кубический_м_нетто  = 0.0;
  позиция_измерения
    Choice NoCheck first
    Convert_end
      длина            = 0;
      диаметр_н        = 0;
      диаметр_к        = 0;
$End

$Pattern  Образец_Бревно_переходит_К3_Т3  : operation  trace
$Relevant_resources
  некоторый_пилоблок_бревна : Пилоблоки         Keep      Keep
  конвейер                  : Конвейер_3        Keep      Keep
  транспортер               : Транспортер_3     NoChange  Keep
  конвейер_2                : Конвейер_2        NoChange  Keep
  показать_бревно           : Бревно_показать   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  некоторый_пилоблок_бревна
    Choice from некоторый_пилоблок_бревна.код_места == Кн_3 and
                Конвейер_3.позиция - некоторый_пилоблок_бревна.позиция -
                (Конвейер_3.счетчик_оборотов - некоторый_пилоблок_бревна.счетчик_оборотов - 1) *
                Конвейер_3.длина <= 200
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Тр_3;
      позиция          = транспортер.позиция;
      счетчик_оборотов = транспортер.счетчик_оборотов;
  конвейер
    Choice from конвейер.текущее_кол_бревен > 0 and
                конвейер.движение == нет
    first
    Convert_begin
      статус           = остановлен;
    Convert_end
      текущее_кол_бревен  = конвейер.текущее_кол_бревен - 1;
      статус           = вперед;
  транспортер
    Choice NoCheck first
    Convert_end
      текущее_кол_бревен  = транспортер.текущее_кол_бревен + 1;
  конвейер_2
    Choice NoCheck first
    Convert_end
      статус           = вперед;
  показать_бревно
    Choice from показать_бревно.номер == некоторый_пилоблок_бревна.номер
    first
    Convert_end
      код_места        = некоторый_пилоблок_бревна.код_места;
      счетчик_оборотов = некоторый_пилоблок_бревна.счетчик_оборотов;
      позиция          = некоторый_пилоблок_бревна.позиция;
$End

$Pattern  Образец_Бревно_переходит_К3_С30  : operation  trace
$Relevant_resources
  некоторое_бревно    : Бревно            Keep      Keep
  конвейер            : Конвейер_3        Keep      Keep
  некоторый_склад     : Склад             NoChange  Keep
  показать_бревно     : Бревно_показать   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Кн_3 and
                Конвейер_3.позиция - некоторое_бревно.позиция -
                (Конвейер_3.счетчик_оборотов - некоторое_бревно.счетчик_оборотов - 1) *
                Конвейер_3.длина <= 200
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Ск_30;
  конвейер
    Choice from конвейер.текущее_кол_бревен > 0 and
                конвейер.движение == нет
    first
    Convert_begin
      статус           = остановлен;
    Convert_end
      текущее_кол_бревен  = конвейер.текущее_кол_бревен - 1;
      статус           = вперед;
  некоторый_склад
    Choice from некоторый_склад.код == Ск_30
    first
    Convert_end
      текущее_кол_бревен  = некоторый_склад.текущее_кол_бревен + 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторое_бревно.номер
    first
    Convert_end
      номер            = 0;
      длина            = 0;
$End

$Pattern Образец_уничтожение_остатка_бревна  : rule   trace
$Relevant_resources
  система           : Распилочная_машина_1   Keep
  некоторое_бревно  : Бревно                 Erase
$Body
  система
    Choice NoCheck first
    Convert_rule
      бревна_обработаны      = система.бревна_обработаны + 1;
      общая_длина_бревен     = система.общая_длина_бревен + некоторое_бревно.нач_длина / 100.0;
      общая_длина_остатка    = система.общая_длина_остатка + некоторое_бревно.длина / 100.0;
      общий_объем_кубический_м_бревен = система.общий_объем_кубический_м_бревен + некоторое_бревно.нач_объем_кубический_м;
      общий_объем_кубический_м_остатка   = система.общий_объем_кубический_м_остатка + некоторое_бревно.объем_кубический_м;
  некоторое_бревно
    Choice from некоторое_бревно.код_места == Ск_30 and
                некоторое_бревно.статус == готово
    first
$End

$Pattern  Образец_Бревно_переходит_Т3_К4  : operation  trace
$Relevant_resources
  транспортер               : Транспортер_3     Keep      Keep
  некоторый_пилоблок_бревна : Пилоблоки         Keep      Keep
  конвейер                  : Конвейер_4        NoChange  Keep
  показать_бревно           : Бревно_показать   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  транспортер
    Choice from транспортер.текущее_кол_бревен > 0 and
                транспортер.движение == нет
    first
    Convert_begin
      статус           = остановлен;
    Convert_end
      текущее_кол_бревен  = транспортер.текущее_кол_бревен - 1;
      статус           = вперед;
  некоторый_пилоблок_бревна
    Choice from некоторый_пилоблок_бревна.код_места == Тр_3 and
                Транспортер_3.позиция - некоторый_пилоблок_бревна.позиция -
                (Транспортер_3.счетчик_оборотов - некоторый_пилоблок_бревна.счетчик_оборотов - 1) *
                Транспортер_3.длина <= 0
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Кн_4;
      позиция          = конвейер.позиция + 200;
      счетчик_оборотов = конвейер.счетчик_оборотов;
  конвейер
    Choice from конвейер.текущее_кол_бревен == 0
    first
    Convert_end
      текущее_кол_бревен  = конвейер.текущее_кол_бревен + 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторый_пилоблок_бревна.номер
    first
    Convert_end
      код_места        = некоторый_пилоблок_бревна.код_места;
      счетчик_оборотов = некоторый_пилоблок_бревна.счетчик_оборотов;
      позиция          = некоторый_пилоблок_бревна.позиция;
$End

$Pattern  Образец_Бревно_переходит_К4_К5  : operation  trace
$Relevant_resources
  конвейер_откуда           : Конвейер_4        Keep      Keep
  некоторый_пилоблок_бревна : Пилоблоки         Keep      Keep
  конвейер_куда             : Конвейер_5        NoChange  Keep
  показать_бревно           : Бревно_показать   NoChange  Keep
$Time = Время_передачи_бревна
$Body
  конвейер_откуда
    Choice from конвейер_откуда.текущее_кол_бревен > 0 and
                конвейер_откуда.движение == нет
    first
    Convert_begin
      статус           = остановлен;
    Convert_end
      текущее_кол_бревен  = конвейер_откуда.текущее_кол_бревен - 1;
      статус           = вперед;
  некоторый_пилоблок_бревна
    Choice from некоторый_пилоблок_бревна.код_места == Кн_4 and
                Конвейер_4.позиция - некоторый_пилоблок_бревна.позиция -
                (Конвейер_4.счетчик_оборотов - некоторый_пилоблок_бревна.счетчик_оборотов - 1) *
                Конвейер_4.длина <= некоторый_пилоблок_бревна.длина
    first
    Convert_begin
      код_места        = проходит;
    Convert_end
      код_места        = Кн_5;
      позиция          = конвейер_куда.позиция - некоторый_пилоблок_бревна.длина;
      счетчик_оборотов = конвейер_куда.счетчик_оборотов;
  конвейер_куда
    Choice NoCheck first
    Convert_end
      текущее_кол_бревен  = конвейер_куда.текущее_кол_бревен + 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторый_пилоблок_бревна.номер
    first
    Convert_end
      код_места        = некоторый_пилоблок_бревна.код_места;
      счетчик_оборотов = некоторый_пилоблок_бревна.счетчик_оборотов;
      позиция          = некоторый_пилоблок_бревна.позиция;
$End

$Pattern  Образец_Движение_конвейера_вперед  : operation
$Relevant_resources
  Данный_конвейер    : Конвейер             Keep      Keep
  позиция_измерения  : Позиция_измерения_1  NoChange  Keep
$Time = Данный_конвейер.шаг / 100.0 / (Данный_конвейер.скорость * 60.0)
$Body
  Данный_конвейер
    Choice from  Данный_конвейер.статус == вперед and
                 Данный_конвейер.движение == нет and
                 Данный_конвейер.текущее_кол_бревен > 0
    first
    Convert_begin
      движение         = да;
    Convert_end
      движение         = нет;
      счетчик_оборотов = Данный_конвейер.счетчик_оборотов +
                           Новый_счетчик_оборотов(вперед, Данный_конвейер.позиция, Данный_конвейер.код);
      позиция          = Новая_позиция(вперед, Данный_конвейер.позиция, Данный_конвейер.код);
  позиция_измерения
    Choice NoCheck first
    Convert_end
      длина            = Новая_длина(Данный_конвейер.код,
                             позиция_измерения.состояние, позиция_измерения.длина);
      диаметр_к        = Новый_диаметр_к(Данный_конвейер.код,
                             позиция_измерения.состояние, позиция_измерения.диаметр_к);
$End

$Pattern  Образец_Движение_конвейера_назад  : operation
$Relevant_resources
  Данный_конвейер : Конвейер      Keep   Keep
$Time = Данный_конвейер.шаг / 100.0 / (Данный_конвейер.скорость * 60.0)
$Body
  Данный_конвейер
    Choice from  Данный_конвейер.статус == назад and
                 Данный_конвейер.движение == нет and
                 Данный_конвейер.текущее_кол_бревен > 0
    first
    Convert_begin
      движение         = да;
    Convert_end
      движение         = нет;
      счетчик_оборотов = Данный_конвейер.счетчик_оборотов +
                           Новый_счетчик_оборотов(назад, Данный_конвейер.позиция, Данный_конвейер.код);
      позиция          = Новая_позиция(назад, Данный_конвейер.позиция, Данный_конвейер.код);
$End

$Pattern  Образец_Движение_транспортера_вперед    : operation
$Relevant_resources
  Данный_транспортер : Транспортер      Keep   Keep
$Time = Шаг_трапспортера / 100.0 / (Данный_транспортер.скорость * 60.0)
$Body
  Данный_транспортер
    Choice from  Данный_транспортер.статус == вперед and
                 Данный_транспортер.движение == нет and
                 Данный_транспортер.текущее_кол_бревен > 0
    first
    Convert_begin
      движение         = да;
    Convert_end
      движение         = нет;
      счетчик_оборотов = Данный_транспортер.счетчик_оборотов +
                           Новый_счетчик_оборотов(вперед, Данный_транспортер.позиция, Данный_транспортер.код);
      позиция          = Новая_позиция(вперед, Данный_транспортер.позиция, Данный_транспортер.код);
$End

$Pattern Образец_уничтожение_пилоблока  : rule   trace
$Relevant_resources
  некоторый_пилоблок_бревна : Пилоблоки        Erase
  конвейер                  : Конвейер_5       Keep
  показать_бревно           : Бревно_показать  Keep
  некоторый_склад           : Склад            Keep
$Body
  некоторый_пилоблок_бревна
    Choice from некоторый_пилоблок_бревна.код_места == Кн_5 and
                Конвейер_5.позиция - некоторый_пилоблок_бревна.позиция -
                (Конвейер_5.счетчик_оборотов - некоторый_пилоблок_бревна.счетчик_оборотов - 1) *
                Конвейер_5.длина <= некоторый_пилоблок_бревна.длина + Конвейер_5.длина -
                Получить_расстояние_склада(некоторый_пилоблок_бревна.код_склада)
    first
  конвейер
    Choice from конвейер.текущее_кол_бревен > 0
    first
    Convert_rule
      текущее_кол_бревен  = конвейер.текущее_кол_бревен - 1;
  показать_бревно
    Choice from показать_бревно.номер == некоторый_пилоблок_бревна.номер
    first
    Convert_rule
      номер            = 0;
      длина            = 0;
  некоторый_склад
    Choice from некоторый_склад.код == некоторый_пилоблок_бревна.код_склада
    first
    Convert_rule
      текущее_кол_бревен  = некоторый_склад.текущее_кол_бревен + 1;
$End

$Pattern Образец_завершение_заказа  : rule   trace
$Relevant_resources
  некий_заказ      : Позиция_заказа        Keep
  некоторый_склад  : Склад                 Keep
  система          : Распилочная_машина_1  Keep
  заказ_для_статис : Заказ_для_статистики  Erase
$Body
  некий_заказ
    Choice from некий_заказ.статус == в_обработке and
                некий_заказ.склад <> проходит and
                некий_заказ.кол_готовых >= некий_заказ.количество
    first
    Convert_rule
      статус           = обработано;
  некоторый_склад
    Choice from некоторый_склад.код == некий_заказ.склад and
                некоторый_склад.текущее_кол_бревен == некий_заказ.кол_пилоблоков
    first
    Convert_rule
      текущее_кол_бревен  = 0;
      номер_заказа     = 0;
      номер_позиции    = 0;
  система
    Choice NoCheck first
    Convert_rule
      заказы_обработаны = система.заказы_обработаны + 1;
  заказ_для_статис
    Choice from заказ_для_статис.номер_заказа == некий_заказ.номер_заказа and
                заказ_для_статис.номер_позиции == некий_заказ.номер_позиции
    first
$End

