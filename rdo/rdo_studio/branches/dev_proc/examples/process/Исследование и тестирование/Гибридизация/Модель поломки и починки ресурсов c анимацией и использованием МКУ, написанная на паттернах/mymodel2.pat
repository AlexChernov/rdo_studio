$Pattern Образец_Прибытие_Заготовок : irregular_event trace
$Relevant_resources
	Новая_Заготовка		:Детали 	Create
	Входной_Накопитель	:ВхНак	Keep
$Time = Интервал_прихода_заготовок(150)
$Body
Новая_Заготовка
	Convert_event trace
		Положение 	 set ВхНак
		Время		 set Time_now
		
Входной_Накопитель	
	Convert_event 
		длина_очереди set Входной_Накопитель.длина_очереди + 1
$End

$Pattern Образец_Работы_Робота : operation
$Relevant_resources
	Деталь		: Детали	 NoChange	keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
	Робот		: Ресурсы	 keep 		keep
$Time = Время_захвата(8,1) + Время_на_участке(Накопитель1.Положение) + Время_захвата(8,1)
$Body
Деталь
	Choice from Деталь.Положение = ВхНак or Деталь.Положение = ВыхНакСт1 or Деталь.Положение = ВыхНакСт2
	with_min	Деталь.Время
	Convert_end 
		Положение set Новый_накопитель(Деталь.Положение)
		Время	  set Time_now

Накопитель1
	Choice from Накопитель1.Положение = Деталь.Положение and Накопитель1.длина_очереди > 0
	Convert_begin
		длина_очереди set Накопитель1.длина_очереди - 1

Накопитель2
	Choice from Накопитель2.Положение = Новый_накопитель(Накопитель1.Положение)
	Convert_end
		длина_очереди set Накопитель2.длина_очереди + 1

Робот	
	Choice from Робот.Индекс = 5 and Робот.Состояние = Свободен
	Convert_begin
		Положение set Новое_положение(Деталь.Положение)
		Состояние set Занят
	Convert_end
		Состояние set Свободен
$End

$Pattern Образец_Работы_Ст1 : operation
$Relevant_resources
	Станок		: Ресурсы	 keep 		keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
	Деталь		: Детали	 keep 		keep
$Time = Обработка_на_станке_1(60,10)
$Body
Станок	
	Choice from Станок.Состояние = Свободен and (Станок.Индекс = 1 or Станок.Индекс = 2 or Станок.Индекс = 3)
	Convert_begin
		Состояние set Занят
	Convert_end
		Состояние set Свободен

Накопитель1
	Choice from Накопитель1.Положение = ВхНакСт1 and Накопитель1.длина_очереди > 0
	Convert_begin
		длина_очереди set Накопитель1.длина_очереди - 1
	
Накопитель2
	Choice from Накопитель2.Положение = ВыхНакСт1 
	Convert_end
		длина_очереди set Накопитель2.длина_очереди + 1
		
Деталь
	Choice from Деталь.Положение = ВхНакСт1 
	with_min	Деталь.Время
	Convert_begin
		Положение set Станок.Положение
	Convert_end 
		Положение set ВыхНакСт1
		Время	  set Time_now
$End

$Pattern Образец_Работы_Ст2 : operation
$Relevant_resources
	Станок		: Ресурсы	 keep 		keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
	Деталь		: Детали	 keep 		keep
$Time = Обработка_на_станке_2(100)
$Body
Станок	
	Choice from Станок.Состояние = Свободен and Станок.Индекс = 4
	Convert_begin
		Состояние set Занят
	Convert_end
		Состояние set Свободен

Накопитель1
	Choice from Накопитель1.Положение = ВхНакСт2
	Convert_begin
		длина_очереди set Накопитель1.длина_очереди - 1
	
Накопитель2
	Choice from Накопитель2.Положение = ВыхНакСт2
	Convert_end
		длина_очереди set Накопитель2.длина_очереди + 1
		
Деталь
	Choice from Деталь.Положение = ВхНакСт2 
	with_min	Деталь.Время
	Convert_begin
		Положение set Станок.Положение
	Convert_end 
		Положение set ВыхНакСт2
		Время	  set Time_now
$End




$Pattern  Образец_Поломка_Станка : keyboard
$Parameters
	Индекс: integer
$Relevant_resources
	Станок : Ресурсы Keep NoChange
$Time = 0.0
$Body
Станок
	Choice from Станок.Индекс = Индекс
	Convert_begin
		Состояние set новое_состояние11(Станок.Состояние)
$End

$Pattern  Образец_Поломка_Станка2 : keyboard
$Relevant_resources
	Станок : Ст2	Keep NoChange
$Time = 0.0
$Body
Станок
	Convert_begin
		Состояние set новое_состояние2(Станок.Состояние)
$End

$Pattern  Образец_Поломка_Робота : keyboard 
$Relevant_resources
	Робот : Робот	Keep NoChange
$Time = 0.0
$Body
Робот
	Convert_begin
		Состояние set новое_состояние_робота(Робот.Состояние)
$End

/*
$Pattern  Образец_Починка : keyboard
$Relevant_resources
	rel_ST11 : ST11	Keep NoChange
$Time = 0.0
$Body
	rel_ST11
	Choice from rel_ST11.Состояние = Сломан
	first
	Convert_begin
		Состояние set Свободен
$End*/