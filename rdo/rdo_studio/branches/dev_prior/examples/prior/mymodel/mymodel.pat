$Pattern Образец_поступления_новой_детали : irregular_event trace
$Relevant_resources
	_Система       : Система  Keep
	_Деталь        : Детали   Create
$Time = Интервал_поступления( 2 )
$Body
_Система
	Convert_event
		количество_деталей set _Система.количество_деталей + 1

_Деталь
	Convert_event
		номер                set _Система.количество_деталей
		ориентирование       set Ориентирование_детали
		состояние            set Свободна
$End

$Pattern Образец_захвата_детали_роботом : operation trace
$Relevant_resources
	_Система  : Система  Keep NoChange
	_Деталь   : Детали   Keep Keep
	_Робот    : Робот    Keep Keep
$Time = Длительность_захвата_детали( 0.2, 0.02 )
$Body
_Система
	Choice from _Система.количество_деталей > 0
	Convert_begin
		количество_деталей  set _Система.количество_деталей - 1

_Деталь
	Choice from _Деталь.состояние = Свободна
	with_min( _Деталь.номер )
	Convert_begin
		состояние  set Захват_начат
	Convert_end
		состояние  set Захват_закончен

_Робот
	Choice from _Робот.состояние = Свободен
	Convert_begin
		состояние  set Захват_начат
	Convert_end
		состояние  set Захват_закончен
$End

$Pattern Образец_ориентирования_детали_по_часовой_стрелке : operation trace
$Relevant_resources
	_Деталь   : Детали   Keep Keep
	_Робот    : Робот    Keep Keep
$Time = Длительность_ориентирования_детали_по_часовой_стрелке( _Деталь.ориентирование )
$Body
_Деталь
	Choice from _Деталь.состояние = Захват_закончен
	Convert_begin
		состояние  set Ориентирование_начато
	Convert_end
		состояние  set Ориентирование_закончено

_Робот
	Choice from _Робот.состояние = Захват_закончен
	Convert_begin
		состояние  set Ориентирование_начато
	Convert_end
		состояние  set Ориентирование_закончено
$End

$Pattern Образец_ориентирования_детали_против_часовой_стрелки : operation trace
$Relevant_resources
	_Деталь   : Детали   Keep Keep
	_Робот    : Робот    Keep Keep
$Time = Длительность_ориентирования_детали_против_часовой_стрелки( _Деталь.ориентирование )
$Body
_Деталь
	Choice from _Деталь.состояние = Захват_закончен
	Convert_begin
		состояние  set Ориентирование_начато
	Convert_end
		состояние  set Ориентирование_закончено

_Робот
	Choice from _Робот.состояние = Захват_закончен
	Convert_begin
		состояние  set Ориентирование_начато
	Convert_end
		состояние  set Ориентирование_закончено
$End

$Pattern Образец_транспортировки_детали_роботом : operation trace
$Relevant_resources
	_Деталь   : Детали   Keep Keep
	_Робот    : Робот    Keep Keep
$Time = Длительность_транспортировки_детали( 0.4, 0.04 )
$Body
_Деталь
	Choice from _Деталь.состояние = Ориентирование_закончено
	Convert_begin
		состояние  set Транспортировка_начата
	Convert_end
		состояние  set Транспортировка_закончена

_Робот
	Choice from _Робот.состояние = Ориентирование_закончено
	Convert_begin
		состояние  set Транспортировка_начата
	Convert_end
		состояние  set Транспортировка_закончена
$End

$Pattern Образец_возврата_робота : operation trace
$Relevant_resources
	_Робот    : Робот    Keep Keep
$Time = Длительность_возврата_робота( 0.4, 0.04 )
$Body
_Робот
	Choice from _Робот.состояние = Транспортировка_закончена
	Convert_begin
		состояние  set Возврат
	Convert_end
		состояние  set Свободен
$End
